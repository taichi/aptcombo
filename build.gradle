plugins {
	id 'java'
	id 'eclipse'
	id 'net.ltgt.apt' version '0.4'
}

repositories {
	maven {
		url "https://maven-central.storage.googleapis.com"
	}
	jcenter()
	mavenCentral()
}

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(AbstractCompile)*.options*.encoding = "UTF-8"

test {
	systemProperty("java.awt.headless", "true")
}

dependencies {
	compile 'ch.qos.logback:logback-classic:1.1.2'
	compile 'com.zaxxer:HikariCP:2.2.5'
	testCompile 'junit:junit:4.+'
	testCompile 'org.jmockit:jmockit:1.+'
	testCompile 'com.h2database:h2:1.+'
}



if (!project.hasProperty('daggerVersion')) {
	ext {
		daggerVersion = "1.2.2"
	}
}

if (!project.hasProperty('domaVersion')) {
	ext {
		domaVersion = "2.5.1"
	}
}

dependencies {
	def doma = "org.seasar.doma:doma:${project.domaVersion}"
	compile doma
	apt doma
	
	compile "com.squareup.dagger:dagger:${project.daggerVersion}"
	def dc = "com.squareup.dagger:dagger-compiler:${project.daggerVersion}"
	apt dc
	testApt dc
	
	def lombok = 'org.projectlombok:lombok:1.16.6'
	apt lombok
	compileOnly lombok
}

processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

task delombok {
	// lombok outputs .class file to directory.
	// but some of other Annotation Processors need that source codes.
	// so delombok make it.
	// see. http://projectlombok.org/features/delombok.html
	ant.taskdef(
		name: 'delombok', 
		classname: 'lombok.delombok.ant.Tasks$Delombok', 
		classpath: configurations.apt.asPath)
	doLast {
		ant.delombok(encoding: 'UTF-8', 
			classpath: sourceSets.main.compileClasspath.asPath,
			//verbose: true,
			to: temporaryDir
		) {
			compileJava.source.matching {
				exclude '**/*Module.java'
			}.addToAntBuilder(ant, 'fileset', FileCollection.AntType.FileSet)
		}
	}
}

compileJava {
	dependsOn delombok
	doFirst {
		def delomboked = fileTree(dir: delombok.temporaryDir);
		def relativized = delomboked.collect {
			delombok.temporaryDir.toPath().relativize(it.toPath())
		}
		source = source.filter { src ->
			relativized.any { src.toPath().endsWith(it) } == false
		}
		source += delomboked
	}
}

eclipse {
	classpath {
		containers = [
			'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
		]
	}
}
